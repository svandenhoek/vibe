# Default DisGeNET prefixes.
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX so: <http://www.sequenceontology.org/miso/current_svn/term/SO:>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX wi: <http://purl.org/ontology/wi/core#>
PREFIX eco: <http://purl.obolibrary.org/obo/eco.owl#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX pav: <http://purl.org/pav/>
PREFIX obo: <http://purl.obolibrary.org/obo/> 

# Custom prefixes.
PREFIX umls: <http://linkedlifedata.com/resource/umls/id/> # DisGeNET
PREFIX ncbigene: <http://identifiers.org/ncbigene/> # DisGeNET
PREFIX pda: <http://rdf.disgenet.org/resource/pda/> # DisGeNET
PREFIX gda: <http://rdf.disgenet.org/resource/gda/> # DisGeNET
PREFIX ordo: <http://www.orpha.net/ORDO/> # DisGeNET / Orphanet
PREFIX hoom: <http://www.semanticweb.org/ontology/HOOM#> # Orphanet

# Query.
CONSTRUCT {
	?hpo rdf:type sio:SIO_010056 ;
	skos:exactMatch ?directMatchDisease ;
	sio:SIO_000001 ?pda , ?orphanet . # possibly other predicate should be used?

	?pda rdf:type sio:SIO_000897 ;
	sio:SIO_000628 ?pdaDisease ;
	sio:SIO_000253 ?pdaSource .

	?orphanet rdfs:subClassOf hoom:Orpha_Num ;
	skos:exactMatch ?orphanetDisease .
}
WHERE {
	{
		# Diseases that are actually UMLS-codes that represent a phenotype and are an exact match with an HPO.
		?directMatchDisease skos:exactMatch ?hpo .

		FILTER EXISTS { ?directMatchDisease rdf:type ncit:C7057 . }
		FILTER ( STRSTARTS(str(?hpo), "http://purl.obolibrary.org/obo/HP_") ) # phenotype.ttl is from DisGeNET v5.0, might not include all exact matches present in v6.0 (v6.0 has no phenotype.ttl).
	}
	UNION
	{
		# Diseases that have a phenotype-disease association according to sources used in DisGeNET v5.0.
		?pda sio:SIO_000628 ?hpo , ?pdaDisease ;
		sio:SIO_000253 ?pdaSource .

		FILTER EXISTS { ?pda rdf:type sio:SIO_000897 . }
		FILTER EXISTS { ?hpo rdf:type sio:SIO_010056 . }
		FILTER EXISTS { ?pdaDisease rdf:type ncit:C7057 . }
		FILTER EXISTS { ?pdaSource rdf:type dctypes:Dataset , dcat:Distribution . }
		FILTER ( STRSTARTS(str(?pda), str(pda:)) ) # As vda's also have an sio:SIO_000628 to diseases and are rdf:type sio:SIO_010056, explicit filter is needed.
	}
	UNION
	{
		# Diseases linked linked to phenotypes according to Orphanet (HPO - ORDO Ontological Module).
		{
			SELECT ?hpo ?orphanet
			WHERE {
				?association owl:equivalentClass/owl:intersectionOf/rdf:rest*/rdf:first ?hpoItem , ?orphaItem .
				?hpoItem owl:someValuesFrom ?hpo .
				?orphaItem owl:someValuesFrom ?orphanet .

				FILTER EXISTS { ?association rdfs:subClassOf hoom:Association . }
				FILTER EXISTS { ?hpoItem owl:onProperty <http://purl.org/oban/association_has_object> . }
				FILTER EXISTS { ?orphaItem owl:onProperty <http://purl.org/oban/association_has_subject> . }
			}
		}
		?orphanetDisease skos:exactMatch ?orphanet .

		FILTER EXISTS { ?orphanetDisease rdf:type ncit:C7057 ; void:inDataset <http://rdf.disgenet.org/v6.0.0/void/disease> . }
	}
}